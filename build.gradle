buildscript {
    ext.kotlin_version = '1.3.21'
    ext.ktor_version = '1.1.3'

    repositories {
        maven { url "https://cache-redirector.jetbrains.com/repo1.maven.org/maven2" }
        maven { url "https://cache-redirector.jetbrains.com/jcenter.bintray.com" }

        // In case of issues with cache redirector:
        // jcenter()
    }

    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    maven { url "https://dl.bintray.com/jetbrains-research/maven" }
    jcenter()
}

apply plugin: 'kotlin'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8
targetCompatibility = sourceCompatibility
compileKotlin { kotlinOptions.jvmTarget = sourceCompatibility }
compileTestKotlin { kotlinOptions.jvmTarget = sourceCompatibility }

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-support-jdk8:0.1'
    
    compile 'log4j:log4j:1.2.17'
    compile("ch.qos.logback:logback-classic:1.2.3")
    
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-jackson:$ktor_version"

    compile group: 'com.github.haifengl', name: 'smile-core', version: '1.5.2'
    compile 'com.opencsv:opencsv:4.5'
    compile group: 'nz.ac.waikato.cms.weka', name: 'weka-stable', version: '3.8.3'
    compile group: 'com.epam', name: 'parso', version: '2.0.9'
    compile group: 'org.apache.poi', name: 'poi', version: '3.9'
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: '3.9'


    compile 'org.jetbrains.bio:bioinf-commons:0.0.1'
    compile 'org.nield:kotlin-statistics:1.2.1'

    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
}


tasks.withType(Test) {
    // Continue execution even if tests for some of the
    // sub-projects failed.
    ignoreFailures = true
    maxParallelForks = Runtime.runtime.availableProcessors()
    maxHeapSize = "1024m"

    testLogging.showStandardStreams = true

    systemProperty "teamcity.build.checkoutDir", System.getProperty("teamcity.build.checkoutDir")
}

test {
    include '**/*Test.class'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

shadowJar {
    // File name: "$baseName-$version-$classifier.jar"
    baseName = 'span'
    version = "${version}.${project.buildCounter}"
    classifier = ''
    minimize {
        // Fix log4j:ERROR Could not instantiate class [org.apache.log4j.xml.DOMConfigurator]
        exclude(dependency('log4j:log4j:1.2.17'))
    }
}

jar {
    manifest {
        attributes provider: 'gradle'
        attributes 'Application-Name': "SPAN $version"
        attributes 'Built-By': 'JetBrains Research TeamCity'
    }
}

task wrapper(type: Wrapper, overwrite: true) {
    gradleVersion = '4.10'
}
